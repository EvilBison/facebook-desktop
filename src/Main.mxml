<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark" 
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:composer="com.facebook.desktop.view.composer.*"
	creationComplete="init()"
	visible="false">
	
	<fx:Script>
		<![CDATA[
			import air.net.URLMonitor;
			
			import com.charlesbihis.engine.notification.NotificationConst;
			import com.charlesbihis.engine.notification.NotificationManager;
			import com.facebook.desktop.FacebookDesktopConst;
			import com.facebook.desktop.control.api.GetAdditionalNotifications;
			import com.facebook.desktop.control.api.GetNewsFeed;
			import com.facebook.desktop.control.api.GetNotifications;
			import com.facebook.desktop.control.logging.Logging;
			import com.facebook.desktop.control.preferences.PreferencesManager;
			import com.facebook.desktop.control.system.SystemIcons;
			import com.facebook.desktop.control.updates.UpdateManager;
			import com.facebook.desktop.control.util.Util;
			import com.facebook.desktop.model.Model;
			import com.facebook.desktop.view.window.MessageWindow;
			import com.facebook.graph.FacebookDesktop;
			
			import mx.logging.ILogger;
			import mx.logging.Log;
			import mx.utils.ObjectUtil;
			
			private static const POLL_DELAY:uint = 60000;	// poll every minute
			
			private var model:Model = Model.instance;
			private var notificationManager:NotificationManager = NotificationManager.instance;
			private var log:ILogger = Log.getLogger(className);
			private var pollTimer:Timer = new Timer(POLL_DELAY);
			
			private function init():void
			{
				// set up logging
				Logging.init();
				log.info("\n********* starting new session *********\n");
				log.info("Logging initialized");

				// grab preferences
				PreferencesManager.loadPreferences();
				
				// set up application to start on user-login
				log.info("Setting app to start on user-login: {0}", model.preferences.startAtLogin);
				NativeApplication.nativeApplication.startAtLogin = model.preferences.startAtLogin;
				
				// check online/offline status
				var urlRequest:URLRequest = new URLRequest("http://www.facebook.com/");
				urlRequest.method = "HEAD";
				var urlMonitor:URLMonitor = new URLMonitor(urlRequest);
				urlMonitor.addEventListener(StatusEvent.STATUS, connectionMonitorHandler);
				urlMonitor.start();
				
				// configuring notification sound
				model.notificationSound = new Sound(new URLRequest("/assets/sounds/drop.mp3"));
				model.notificationSound.addEventListener(Event.COMPLETE, loadSoundHandler);
				
				// determine OS
				var os:String = flash.system.Capabilities.os.substr(0, 3);
				if (os.indexOf("Win") >= 0)
				{
					model.operatingSystem = "Win";
				}  // if statement
				else if (os.indexOf("Mac") >= 0)
				{
					model.operatingSystem = "Mac";
					NotificationManager.notificationDisplayLocation = NotificationConst.DISPLAY_LOCATION_TOP_RIGHT;
					
					// Unfortunately, Hindi characters don't render correctly using
					// the Spark component namespace on Mac, so instead of removing
					// support for it entirely, let's just remove support for it on
					// OS X :S  Not sure if this will go over well, but until I can 
					// find a solution that allows text-wrapping and displays
					// characters as expected in all languages, then let's go with
					// this.
					model.locales.pop();
				}  // else-if statement
				else
				{
					model.operatingSystem = "Linux";
				}  // else statement
				log.info("OS set to {0}", model.operatingSystem);
				
				// initialize tray/dock icon
				SystemIcons.init();
				
				// check for updates
				log.info("Checking for updates");
				UpdateManager.checkForUpdates();
				
				// initialize our Facebook session
				FacebookDesktop.init(Model.APPLICATION_ID, loginHandler);
				
				var triedLogin:Boolean = false;
				function loginHandler(session:Object, fail:Object):void
				{
					if (fail == null)
					{
						// if we have a session, restore it
						if (session != null)
						{
							log.info("Connected and all permissions granted! We are fully authorized!");
							
							// store the user
							model.currentUser = session.user;
							
							// adjust icon and menus
							SystemIcons.changeIcon(true);
							SystemIcons.changedLoggedInMenuState(true);
							
							// show login popup
							notificationManager.show(resourceManager.getString("resources", "toast.welcome"), "", FacebookDesktop.getImageUrl(session.user.id), FacebookDesktopConst.FACEBOOK_DESKTOP_PAGE);
							notificationManager.clearLatestFiveUpdates();
							
							// get latest update so that we can start receiving pop-ups for *new* updates
							FacebookDesktop.api("/me/home", getNewsFeedHandler, {limit:1});
						}  // if statement
						else if (!triedLogin)
						{
							FacebookDesktop.login(loginHandler, Model.REQUIRED_PERMISSIONS);
							triedLogin = true;
						}  // else-if statement
					}  // if statement
					else
					{
						log.error("Error logging in.  Error object: " + ObjectUtil.toString(fail));
						var messageWindow:MessageWindow = new MessageWindow();
						messageWindow.windowTitle = "Login Error";
						messageWindow.windowMessage = fail.error.type + ": " + fail.error.message;
						messageWindow.showOkayButton = true;
						messageWindow.open();
					}  // else statement
				}  // loginHandler
				
				function getNewsFeedHandler(result:Object, fail:Object):void
				{
					if (fail == null && result != null && result is Array && (result as Array).length > 0)
					{
						// set latest-story-update time
						var latestNewsFeedUpdate:Date = Util.RFC3339toDate(result[0].created_time);
						log.info("Setting latest news feed update time to " + latestNewsFeedUpdate.toString());
						model.latestNewsFeedUpdate = (latestNewsFeedUpdate.time / 1000).toString();
						
						startTheShow();
					}  // if statement
					else
					{
						log.error("Request to get latest update has failed!  Error object: " + fail);
					}  // else statement
				}  // getNewsFeedHandler
				
				function connectionMonitorHandler(event:Event):void
				{
					model.connected = urlMonitor.available;
					
					// TODO: change system-tray/dock icon and context menus
					if (!urlMonitor.available)
					{
						SystemIcons.changeApplicationToolTip(resourceManager.getString("resources", "application.toolTip.disconnected"));
					}  // if statement
					else
					{
						SystemIcons.changeApplicationToolTip(resourceManager.getString("resources", "application.name"));
					}  // else statement
				}  // connectionMonitorHandler
				
				function loadSoundHandler(event:Event):void
				{
					log.info("Notification sound loaded");
				}  // loadSoundHandler
			}  // init
			
			private function startTheShow():void
			{
				log.info("Initialization and connection complete!");
				
				// check for start-up notifications (likes, comments, group posts, messages, pokes, shares, friend-requests, group-invites, event-invites)
				log.info("Fetching start-up notifications");
				
				// likes, comments, and group posts
				var getNotificationsCommand:GetNotifications = new GetNotifications();
				getNotificationsCommand.execute();
				
				// messages, pokes, shares, friend-requests, group-invites, and event-invites
				var getAdditionalNotificationsCommand:GetAdditionalNotifications = new GetAdditionalNotifications();
				getAdditionalNotificationsCommand.execute(null, null, {isStartup:true});
				
				// finally, start the poller
				log.info("Starting the poller");
				pollTimer.addEventListener(TimerEvent.TIMER, poll);
				pollTimer.start();
				
				function poll(event:TimerEvent = null):void
				{
					if (model.connected && model.currentUser != null && !model.paused)
					{
						log.info("Polling!");
						
						// check for news feed updates
						var getNewsFeedCommand:GetNewsFeed = new GetNewsFeed();
						getNewsFeedCommand.execute({since:model.latestNewsFeedUpdate});
						
						// check for new likes, comments, and group posts
						getNotificationsCommand.execute({since:model.latestNotificationUpdate});
						
						// check for new messages, pokes, shares, friend-requests, group-invites, and event-invites
						getAdditionalNotificationsCommand.execute();
					}  // if statement
				}  // poll
			}  // startTheShow
		]]>
	</fx:Script>
	
	<fx:Metadata>
		[ResourceBundle("resources")]
	</fx:Metadata>
	
	<fx:Style source="/com/facebook/desktop/view/style/style.css" />
	
</s:WindowedApplication>
